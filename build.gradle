plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    withSourcesJar()
    withJavadocJar()
}

group = 'com.myseotoolbox'
version = '4.0.7'

base {
    archivesName.set(rootProject.name)
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-autoconfigure:4.0.1'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor:2.1.0.RELEASE'
    implementation 'com.google.cloud:spring-cloud-gcp-pubsub:7.4.1'
    implementation 'com.google.cloud:spring-cloud-gcp-starter-pubsub:7.4.1'
    implementation 'com.google.guava:guava:27.1-jre'
    implementation 'tools.jackson.core:jackson-databind:3.0.3'

    compileOnly 'org.projectlombok:lombok:1.18.42'
    annotationProcessor 'org.projectlombok:lombok:1.18.42'
}

artifacts {
    archives jar, javadocJar, sourcesJar
}

signing {

    def signingKey = findProperty("signingKey")
    if (signingKey != null) {
        //In CI we have the key stored in ORG_GRADLE_PROJECT_signingKey instead of accessing file specified in
        // local gradle.properties
        def signingPassword = findProperty("signingKeyPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications
}


tasks.named('test') {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'GCP Commons - by My SEO Toolbox'
                description = 'A library to abstract GCP Common libraries'
                url = 'https://github.com/salvatorenovelli/gcp-commons'

                developers {
                    developer {
                        id = 'snovelli'
                        name = 'Salvatore'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com:salvatorenovelli/gcp-commons.git'
                    developerConnection = 'scm:git:git:github.com:salvatorenovelli/gcp-commons.git'
                    url = 'https://github.com/salvatorenovelli/gcp-commons'
                }
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            // url = "https://central.sonatype.com/repository/maven-snapshots/"

            credentials {
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }
}